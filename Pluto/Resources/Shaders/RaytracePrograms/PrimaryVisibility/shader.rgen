#version 460
#extension GL_NV_ray_tracing : require
#define RAYTRACING

#include "Pluto/Resources/Shaders/Common/Descriptors.hxx"
layout(location = 0) rayPayloadNV HitInfo payload;
#include "Pluto/Resources/Shaders/Common/ShaderCommon.hxx"
#include "Pluto/Resources/Shaders/Common/Options.hxx"
#include "Pluto/Resources/Shaders/Common/ShaderConstants.hxx"
#include "Pluto/Resources/Shaders/Common/OffsetRay.hxx"

void main() 
{
    /* Next, Compute ray origin and direction */
    init_random(ivec2(gl_LaunchIDNV.xy), push.consts.frame, 0);
    int max_path_depth = int(push.consts.parameter1);
    const ivec2 pixel_coords = ivec2(gl_LaunchIDNV.xy);

    vec4 temp = imageLoad(gbuffers[SEED_LUMINANCE_ADDR], pixel_coords);
    ivec2 pixel_seed = ivec2(temp.xy);
    int frame_seed = int(temp.z);

    vec3 origin, direction;
    EntityStruct camera_entity = ebo.entities[push.consts.camera_id];
    CameraStruct camera = cbo.cameras[camera_entity.camera_id];
    TransformStruct camera_transform = tbo.transforms[camera_entity.transform_id];
    int multiview_idx = push.consts.viewIndex;
    multiview_idx = 0; // temporary
    get_origin_and_direction(
        pixel_seed, frame_seed,
        camera.multiviews[multiview_idx].projinv,
        camera_transform.localToWorld * camera.multiviews[multiview_idx].viewinv,
        pixel_coords, ivec2(gl_LaunchSizeNV.xy), origin, direction);

    /* Trace a ray to compute primary visibility data */
    vec3 world_position = vec3(INFINITY);
    vec3 model_position = vec3(INFINITY);
    vec3 world_normal = vec3(INFINITY);
    vec3 albedo = vec3(0.0);
    float metallic = 0.;
    float roughness = 1.;
    vec2 uv = vec2(0.0);
    float dist = INFINITY;
    int entity_id = -1;

    int transparency_continues = 0;
    int bounce = 0;
    precise vec3 motion = vec3(0.0);
    bool reset = true;
    do {
        bounce++;
        /* Trace a ray through the scene */
        payload.is_shadow_ray = false;
        payload.entity_id = -1;
        traceNV(topLevelAS, gl_RayFlagsNoneNV, 0xff, 0, 0, 0, origin, TMIN, direction, TMAX, 0);

        /* If we hit the sky... */
        if (payload.entity_id < 0  || payload.entity_id >= MAX_ENTITIES) {
            albedo = get_environment_color(direction);
            break;
        }

        EntityStruct entity;
        entity.initialized = 0;

        MaterialStruct mat; LightStruct light;
        unpack_entity_struct(payload.entity_id, entity);
        unpack_material_struct(payload.entity_id, entity, payload.uv, payload.m_p, mat);
        unpack_light_struct(payload.entity_id, entity, light);

        /* If the object is completely transparent, go through it  */
        if ((mat.base_color.a < EPSILON) && (transparency_continues < MAX_TRANSPARENCY_CONTINUES)) {
            transparency_continues++;
            origin = payload.w_p + EPSILON * direction;
            continue;
        }

        /* If the object is a light, use its light color as albedo */
        // Not sure why, but this causes albedo to be black?
        if ((entity.light_id >= 0) && (entity.light_id < max_lights)) 
        {
            // albedo += vec3(1.0, 1.0, 1.0);//light.color.rgb;// * light.intensity;
        }

        albedo = mat.base_color.rgb;
        metallic = mat.metallic;
        roughness = mat.roughness;
        entity_id = payload.entity_id;
        world_position = payload.w_p;
        model_position = payload.m_p;
        world_normal = payload.w_n;
        dist = payload.distance;
        uv = payload.uv;

        /* Compute motion vector for primary visibility */
        if (entity.initialized == 1) {
            reset = false;
            TransformStruct transform = tbo.transforms[entity.transform_id];
            precise vec4 w_p_prev = transform.localToWorldPrev * vec4(model_position, 1.0);

            precise vec4 v_p_curr = camera.multiviews[multiview_idx].viewproj * camera_transform.worldToLocal * vec4(world_position, 1.0);
            precise vec4 v_p_prev = camera.multiviews[multiview_idx].viewproj * camera_transform.worldToLocalPrev * w_p_prev;

            motion = (v_p_prev.xyz / v_p_prev.w) - (v_p_curr.xyz / v_p_curr.w);
        }
        
        break;
    } while (transparency_continues <= MAX_TRANSPARENCY_CONTINUES);

    

    /* Write to G Buffers */

    // Position G Buffer
    imageStore(gbuffers[POSITION_DEPTH_ADDR], ivec2(gl_LaunchIDNV.xy), vec4(world_position, dist));

    // Normal G Buffer
    imageStore(gbuffers[NORMAL_ID_ADDR], ivec2(gl_LaunchIDNV.xy), vec4(world_normal, entity_id));

    // Seed/luminance
    imageStore(gbuffers[SEED_LUMINANCE_ADDR], ivec2(gl_LaunchIDNV.xy), vec4(pixel_seed, frame_seed, luminance(albedo)));

    // UV
    imageStore(gbuffers[UV_METALLIC_ROUGHESS_ADDR], ivec2(gl_LaunchIDNV.xy), vec4(uv.x, uv.y, metallic, roughness));

    // Raw albedo
    imageStore(gbuffers[ALBEDO_ADDR], ivec2(gl_LaunchIDNV.xy), vec4(albedo, 1.0));

    // Motion Vector G Buffer
    imageStore(gbuffers[MOTION_ADDR], ivec2(gl_LaunchIDNV.xy), vec4(motion, (reset) ? 1.0 : 0.0));
}